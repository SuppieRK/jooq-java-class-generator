plugins {
	id 'io.github.suppierk.jooq-java-class-generator'
}

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.9'

	// jooqGenerator scope comes from jOOQ code generator plugin
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.7.9'
}

// (Almost) standard jOOQ code generator plugin configuration
//
// See https://github.com/etiennestuder/gradle-jooq-plugin?tab=readme-ov-file#configuration
jooq {
	// Allows you to select specific version of jOOQ you want to use
	// https://mvnrepository.com/artifact/org.jooq/jooq
	version = '3.20.5'

	configurations {
		main {
			generationTool {
				logging = org.jooq.meta.jaxb.Logging.WARN

				// jdbc part can be omitted for simplicity

				generator {
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'

						// Optional, but if Flyway doesn't specify one - this schema will be used to run Flyway migration
						inputSchema = 'public'
					}

					target {
						packageName = 'io.github.suppierk.example'
					}
				}
			}
		}
	}
}

jooqCodegen {
	database('postgresDb') {
		driver = 'org.postgresql.Driver'

		container {
			image = 'postgres:17-alpine'
		}

		schema('public') {
			flyway {
				locations = 'classpath:db/migration'
				defaultSchema = 'public'
			}
			jooqConfigurations(['main'])
		}
	}
}
