plugins {
	id 'groovy'
	id 'java-gradle-plugin'

	// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
	id 'com.gradle.plugin-publish' version '1.3.1'

	// https://plugins.gradle.org/plugin/org.nosphere.gradle.github.actions
	id 'org.nosphere.gradle.github.actions' version '1.4.0'
}

group = 'io.github.suppierk'
version = '2.0.0'

def functionalTest = sourceSets.create('functionalTest')

def functionalTestTask = tasks.register('functionalTest', Test) {
	group = 'verification'
	testClassesDirs = functionalTest.output.classesDirs
	classpath = functionalTest.runtimeClasspath
}

tasks.named('check') {
	dependsOn functionalTestTask
}

gradlePlugin {
	website = 'https://github.com/SuppieRK/jooq-java-class-generator'
	vcsUrl = 'https://github.com/SuppieRK/jooq-java-class-generator'

	plugins.register('io.github.suppierk.jooq-java-class-generator') {
		id = 'io.github.suppierk.jooq-java-class-generator'
		implementationClass = 'io.github.suppierk.codegen.GeneratorPlugin'

		displayName = 'jOOQ Class Generator'
		description = 'Generates jOOQ Java classes from the database launched in Docker container after Flyway migrations were applied'
		tags = [
			'generator',
			'jooq',
			'database',
			'flyway',
			'docker'
		]
	}

	testSourceSets functionalTest
}

repositories {
	gradlePluginPortal()
	mavenCentral()
}

ext {
	set('flywayVersion', '11.8.2')
	set('gradleJooqPluginVersion', '10.1')
	set('testContainersVersion', '1.21.0')

	set('jUnitVersion', '5.12.2')
	set('jUnitPlatformLauncherVersion', '1.12.2')
	set('spockVersion', '2.3-groovy-3.0')
}

dependencies {
	// ====================
	// Exposed plugin dependencies

	// https://plugins.gradle.org/plugin/org.flywaydb.flyway
	api group: 'org.flywaydb', name: 'flyway-gradle-plugin', version: flywayVersion

	// https://plugins.gradle.org/plugin/nu.studer.jooq
	api group: 'nu.studer', name: 'gradle-jooq-plugin', version: gradleJooqPluginVersion


	// ====================
	// Implementation dependencies

	// https://mvnrepository.com/artifact/org.testcontainers/postgresql
	implementation group: 'org.testcontainers', name: 'postgresql', version: testContainersVersion

	// https://mvnrepository.com/artifact/org.flywaydb/flyway-database-postgresql
	implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: flywayVersion


	// ====================
	// Test dependencies

	testImplementation gradleTestKit()

	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jUnitVersion

	// https://mvnrepository.com/artifact/org.junit.platform/junit-platform-launcher
	testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: jUnitPlatformLauncherVersion


	// ====================
	// Functional test dependencies

	// https://mvnrepository.com/artifact/org.spockframework/spock-core
	functionalTestImplementation(group: 'org.spockframework', name: 'spock-core', version: spockVersion) {
		exclude group: 'org.codehaus.groovy'
	}

	// https://mvnrepository.com/artifact/org.junit.platform/junit-platform-launcher
	functionalTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: jUnitPlatformLauncherVersion
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
}