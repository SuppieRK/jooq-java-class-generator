import java.nio.charset.StandardCharsets

plugins {
	// https://plugins.gradle.org/plugin/com.diffplug.spotless
	id 'com.diffplug.spotless' version '7.2.0'
}

repositories {
	mavenCentral()
}

allprojects {
	group = 'io.github.suppierk'
	version = '0.0.1-SNAPSHOT'
}

subprojects {
	// Language
	apply plugin: 'java'

	java {
		def javaVersion = JavaVersion.VERSION_21.toString()

		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
		compileJava.options.encoding = StandardCharsets.UTF_8.name()

		withSourcesJar()
		withJavadocJar()

		toolchain {
			languageVersion = JavaLanguageVersion.of(javaVersion)
		}
	}

	tasks.withType(Javadoc).configureEach {
		options.addStringOption('Xdoclint:-missing', '-quiet')
	}
}

spotless {
	java {
		target '**/src/*/java/**/*.java'

		googleJavaFormat()
	}

	groovy {
		target '**/src/**/*.groovy'

		removeSemicolons()
		greclipse()
	}

	groovyGradle {
		target '**/*.gradle'
		greclipse()
	}
}